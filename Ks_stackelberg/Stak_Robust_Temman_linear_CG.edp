/* Authors: Louis Breton <louis.breton@ciencias.unam.mx>,
 * Authors: Cristhian Montoya <cdmontoy@mat.uc.cl>,
 *
 * This program is free software: you can redistribute it and/or modify it
 * under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hopeC that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see
 * <https://www.gnu.org/licenses/>.
 *
 */

include "Ks_stackelberg/problems_macros.edp"
include "Ks_stackelberg/problems_settings.edp"

//////////////////////////////////////////////////////////////

/////////ELEMENTOS //////////////////////
//Definiendo los elementos
FeOmega phi2,phi2test,phi2lap,phi2laptest,phi2old,phi2lapold,phi2oldold; //elementos para el phi2varform
FeOmega phi1,phi1test,phi1lap,phi1laptest,phi1old,phi1lapold,phi1oldold; // elementos para el phi1varform
FeOmega Rhsphi21; 
FeOmega Rhsphi22; 
FeOmega[int] phi2a(maxiteration); // objetivo del control
FeOmega[int] phi1a(maxiteration); // objetivo del control
FeOmega psicontroldual; //elemento de la perturbacion para un tiempo fijo
FeOmegaControlF vcontroldual;	 //elemento del seguidor para un tiempo fijo
FeSpacetimeControlF vdual,gradvdual,w1dual,Bargradvdual;
FeSpacetime phi1sol,phi2sol,psidual,gradpsidual,w2dual,Bargradpsidual;

/////////ELEMENTOS //////////////////////
//Definiendo los elementos
FeOmega uu,utest,ulap,ulaptest,uold,ulapold,uoldold; //elementos para el Uvarform
FeOmega zz,ztest,zlap,zlaptest,zold,zlapold,zoldold; // elementos para el dual
FeOmega uudiff; // elemento para guardar (y-uobj)
FeOmega[int] uua(maxiteration),uulapa(maxiteration); // objetivo del control
FeOmega[int] zza(maxiteration); // objetivo del control
FeOmega psicontrol; //elemento de la perturbacion para un tiempo fijo
FeOmegaControlF vcontrol;	 //elemento del seguidor para un tiempo fijo
FeOmegaControlL hcontrol;	 //elemento del seguidor para un tiempo fijo
FeSpacetimeControlF v,gradv;
FeSpacetimeControlL h,gradh;
FeSpacetime zsol,usol,psi,gradpsi;
FeSpacetime uobj=Uobjtivo;



////////////FORMA VARIACIONAL de U///////////////////////
problem  Uvarform([uu,ulap],[utest,ulaptest],init=1) =
int2d(Th,optimize=2)(alpha*uu*utest)
+ int2d(Th,optimize=2)((1-theta)*(
- dx(ulap)*dx(utest)
- dx(uu)*dx(utest)
))
+ int2d(Th,optimize=2)(theta*(
- dx(ulapold)*dx(utest)
- dx(uold)*dx(utest)
))
+ int2d(Th,optimize=2)((3./2.)*uold*dx(uold)*(utest)) 
- int2d(Th,optimize=2)((1./2.)*uoldold*dx(uoldold)*(utest))


-int2d(Th,FollowerRegionLabel,IntersecRegionLabel,optimize=2)( utest*vcontrol)
-int2d(Th,optimize=2)( utest*psicontrol)            
-int2d(Th,LiderRegionLabel,IntersecRegionLabel,optimize=2)( utest*hcontrol)            

+int2d(Th,optimize=2)(dx(uu)*dx(ulaptest))
+int2d(Th,optimize=2)( ulap*ulaptest)    

+int2d(Th,optimize=2)( -alpha*utest*uold)           
+ on(2,4,uu=0.0)  // Condiciones de frontera
;


////////////FORMA VARIACIONAL de Z///////////////////////
problem  Zvarform([zz,zlap],[ztest,zlaptest],init=1) =
int2d(Th,optimize=2)(alpha*zz*ztest)
+ int2d(Th,optimize=2)((1-theta)*(
- dx(zlap)*dx(ztest)
- dx(zz)*dx(ztest)
))
+ int2d(Th,optimize=2)(theta*(
- dx(zlapold)*dx(ztest)
- dx(zold)*dx(ztest)
))
- int2d(Th,optimize=2)((3./2.)*uold*dx(zold)*(ztest)) 
+ int2d(Th,optimize=2)((1./2.)*uoldold*dx(zoldold)*(ztest)) 

+ int2d(Th,optimize=2)(dx(zz)*dx(zlaptest))
+ int2d(Th,optimize=2)( zlap*zlaptest)    

+int2d(Th,optimize=2)( mu*ztest*uudiff)   
+int2d(Th,optimize=2)( -alpha*ztest*zold)

+ on(2,4,zz=0.0)  // Condiciones de frontera
;


////////////FORMA VARIACIONAL de phi1///////////////////////
problem  phi1varform([phi1,phi1lap],[phi1test,phi1laptest],init=1) =
int2d(Th,optimize=2)(alpha*phi1*phi1test)
+ int2d(Th,optimize=2)((1-theta)*(
- dx(phi1lap)*dx(phi1test)
- dx(phi1)*dx(phi1test)
))
+ int2d(Th,optimize=2)(theta*(
- dx(phi1lapold)*dx(phi1test)
- dx(phi1old)*dx(phi1test)
))

+ int2d(Th,optimize=2)((3./2.)*uold*(phi1old)*dx(phi1test)) 
- int2d(Th,optimize=2)((1./2.)*uoldold*(phi1oldold)*dx(phi1test)) 

-int2d(Th,FollowerRegionLabel,IntersecRegionLabel,optimize=2)( phi1test*vcontroldual)
-int2d(Th,optimize=2)( phi1test*psicontroldual)            


+ int2d(Th,optimize=2)(dx(phi1)*dx(phi1laptest))
+ int2d(Th,optimize=2)( phi1lap*phi1laptest)    

+int2d(Th,optimize=2)( -alpha*phi1test*phi1old)

+ on(2,4,phi1=0.0)  // Condiciones de frontera
;


////////////FORMA VARIACIONAL de phi2///////////////////////
problem  phi2varform([phi2,phi2lap],[phi2test,phi2laptest],init=1) =
int2d(Th,optimize=2)(alpha*phi2*phi2test)
+ int2d(Th,optimize=2)((1-theta)*(
- dx(phi2lap)*dx(phi2test)
- dx(phi2)*dx(phi2test)
))
+ int2d(Th,optimize=2)(theta*(
- dx(phi2lapold)*dx(phi2test)
- dx(phi2old)*dx(phi2test)
))
- int2d(Th,optimize=2)((3./2.)*uold*dx(phi2old)*(phi2test)) 
+ int2d(Th,optimize=2)((1./2.)*uoldold*dx(phi2oldold)*(phi2test))

-int2d(Th,optimize=2)( phi2test*Rhsphi21)   
-int2d(Th,optimize=2)( phi2test*Rhsphi22)   


+ int2d(Th,optimize=2)(dx(phi2)*dx(phi2laptest))
+ int2d(Th,optimize=2)( phi2lap*phi2laptest)    

+int2d(Th,optimize=2)( -alpha*phi2test*phi2old)           
+ on(2,4,phi2=0.0)  // Condiciones de frontera
;




/////Macro para resolver el problema Uvarform//
macro SolUvarform(vs,psis,hs) 
uold = Uini;
uoldold = uold;
ulapold = Uinixx;
psicontrol = psis(x,dt);	
vcontrol = vs(x,dt);	
hcontrol = hs(x,dt);	
Uvarform;
uua[0] = uu;
uulapa[0] =  ulap; 	 
uold = uu;
ulapold = ulap;
for (int i=1;i<maxiteration;i++) {
real currenttime = (i+1)*dt;
psicontrol = psis(x,currenttime);	
vcontrol = vs(x,currenttime);
hcontrol = hs(x,currenttime);		
Uvarform;
uua[i] = uu;
uulapa[i] =  ulap; 	 
uoldold = uold;  	 
uold = uu;
ulapold = ulap;	
};
//EOM

macro SolZvarform(uuas,Uoobj)
zold = 0.0;
zoldold = 0.0;
zlapold = 0.0;
zza[maxiteration-1] = 0.0;
uold = uuas[maxiteration-1];
uoldold = uuas[maxiteration-1];
uudiff  = uuas[maxiteration-1]-Uoobj(x,Tfinal);	
Zvarform;
zza[maxiteration-2] = zz;
zoldold = zold;  	 
zold = zz;
zlapold = zlap;	
for (int i=2;i<maxiteration;i++) {
uold = uuas[maxiteration-i+1];
uoldold = uuas[maxiteration-i];	
uudiff = uold-Uoobj(x,Tfinal-dt*(i-1));	
Zvarform;
zza[maxiteration-i-1] = zz;
zoldold = zold;  	 
zold = zz;
zlapold = zlap;	 
};  
//EOM

/////Macro para resolver el problema phi1varform//
macro Solphi1varform(uuas,vs,psis) 
phi1old = 0;
phi1oldold = 0;
phi1lapold = 0;
uold = uuas[0];
uoldold = uuas[0];
psicontroldual = psis(x,dt);	
vcontroldual = vs(x,dt);	
phi1varform;
phi1a[0] = phi1;  	 
phi1old = phi1;
phi1lapold = phi1lap;
for (int i=1;i<maxiteration;i++) {
real currenttime = (i+1)*dt;
psicontroldual = psis(x,currenttime);	
vcontroldual = vs(x,currenttime);
uold = uuas[i];
uoldold = uuas[i-1];	
phi1varform;
phi1a[i] = phi1;
phi1oldold = phi1old;  	 
phi1old = phi1;
phi1lapold = phi1lap;	
};
//EOM


macro Solphi2varform(beta,uuas,uulapas,zzas,Uoobj,Uoobjxx,phi1as)
phi2old = -beta*(uuas[maxiteration-1]-Uoobj(x,Tfinal));
phi2oldold = -beta*(uuas[maxiteration-1]-Uoobj(x,Tfinal));
phi2lapold = -beta*(uulapas[maxiteration-1]-Uoobjxx(x,Tfinal));
phi2a[maxiteration-1] = -beta*(uuas[maxiteration-1]-Uoobj(x,Tfinal));
uold = uuas[maxiteration-1];
uoldold = uuas[maxiteration-1];
Rhsphi21  = -mu*phi1as[maxiteration-1];	
Rhsphi22  = dx(zzas[maxiteration-1])*phi1as[maxiteration-1];	
phi2varform;
phi2a[maxiteration-2] = phi2;
phi2oldold = phi2old;  	 
phi2old = phi2;
phi2lapold = phi2lap;	
for (int i=2;i<maxiteration;i++) {
uold = uuas[maxiteration-i+1];
uoldold = uuas[maxiteration-i];	
Rhsphi21  = -mu*phi1as[maxiteration-i+1];	
Rhsphi22  = dx(zzas[maxiteration-i+1])*phi1as[maxiteration-i+1];	
phi2varform;
phi2a[maxiteration-i-1] = phi2;
phi2oldold = phi2old;  	 
phi2old = phi2;
phi2lapold = phi2lap;	 

};  
//EOM





//////////////////////////////////////////////////
/////////////////MACRO DE CG PARA EL DUAL////////
macro LinearCGRobustControl(vs,psis,uuas,uulapas,zzas,Uoobj,Uoobjxx)
{
/*variables for the VG*/
bool salirRobust = 0; 
real pasograd = 0.0;
real g0 = 0.0;
real gn = 0.0;
real gn1= 0.0;
real ynn= 0.0;

cout << endl;
cout << "Beginning CG method for the dual problem" << endl;

/*Affine Primal*/
Solphi1varform(uuas,vs,psis);
/*FillspaceElement(phi1sol,phi1a);*/

/*Affine Dual*/
Solphi2varform(beta1,uuas,uulapas,zzas,Uoobj,Uoobjxx,phi1a);

/*Fill space time */	
FillspaceElement(phi2sol,phi2a);

/*DJ/D(k,s)(v,psi)*/			
gradientes(gradvdual,gradpsidual,vs,psis,phi2sol); 

/*Ini the w elements*/
w1dual = gradvdual;
w2dual = gradpsidual;

innerproduct(ThSpacetimeControlF,gradvdual,gradvdual,g0);
innerproduct(ThSpacetime,gradpsidual,gradpsidual,g0);

cout << "Inicial gradient norm = " << g0 << endl;

while(!salirRobust){
/*Linear Primal */	
Solphi1varform(uuas,w1dual,w2dual);
/*Linear dual beta = 0*/	
Solphi2varform(0.0,uuas,uulapas,zzas,Uoobj,Uoobjxx,phi1a);

/*Fill space time */	
FillspaceElement(phi2sol,phi2a);

/*BARDJ/D(k,s)(w1,w2)*/		
gradientes(Bargradvdual,Bargradpsidual,w1dual,w2dual,phi2sol); 

/*Optimal descent step*/
gn=0.0;	
real aww=0.0;

/*gn = gn + <gradv,gradv> */
innerproduct(ThSpacetimeControlF,gradvdual,gradvdual,gn);

/*gn = gn + <gradpsi,gradpsi>*/
innerproduct(ThSpacetime,gradpsidual,gradpsidual,gn);

/*aww = aww + <Bargradv,W2>*/
innerproduct(ThSpacetimeControlF,Bargradvdual,w1dual,aww);

/*aww = aww + <Bargradpsi,W1>*/
innerproduct(ThSpacetime,Bargradpsidual,w2dual,aww);

/*Optimal Step*/
pasograd = gn/aww;

cout << "Grad step = "<< pasograd << endl;

/*vs = vs - w1*pasograd*/
descenso(vs,w1dual, pasograd);

/*psis = psis - w2*pasograd*/
descenso(psis,w2dual, pasograd);

/*gradv = Bargradv - Bargradv*pasograd*/
descenso(gradvdual,Bargradvdual, pasograd);

/*gradpsi = gradpsi - Bargradpsi*pasograd*/
descenso(gradpsidual,Bargradpsidual, pasograd);

/*Step 2*/
gn1 = 0.0;

/*gn1 = gn1 + <gradv,gradv> */
innerproduct(ThSpacetimeControlF,gradvdual,gradvdual,gn1);

/*gn1 = gn1 + <gradpsi,gradpsi>*/
innerproduct(ThSpacetime,gradpsidual,gradpsidual,gn1);

cout << "||gn+1|| ^2 = "<< gn1 << endl;
cout << "Current stop condition = "<< sqrt(gn1/g0) << endl;

if(sqrt(gn1/g0) < optitol){
	salirRobust = 1;
	}
	
/*Update step*/
ynn = gn1/gn;
cout << "Current update step = "<< ynn << endl;

/*w1 = gradv + ynn*w1*/
actualiza(w1dual,gradvdual,ynn);

/*w2 = gradpsi + ynn*w2*/
actualiza(w2dual,gradpsidual,ynn);
} 
}//EOM
//////////////////////////////////////////////////
//////////////////////////////////////////////////

//////////////////////////////////////////////////
/////////////////MACRO PARA EL PUNTO SILLA////////
macro NonLinearRobustControl(hs)
{
bool salir = 0; 
real error = 0.0;
real errordesenso = 0; 
real erroracenso = 0; 
real[int] pasov = [0.001]; 
real[int] pasopsi = [0.001]; 

bool check = true;

real optimalidad = 0.; 

int iterationnum = 0;	
cout << endl;
cout << "Beginning Temman method for the saddle problem" << endl;

while(!salir){
	
	SolUvarform(v,psi,hs);
	FillspaceElement(usol,uua)	
	iterationnum++;
	J(usol,v,psi,error); 
   	SolZvarform(uua,uobj);  
	FillspaceElement(zsol,zza)	
   	iterationnum++;
	gradientes(gradv,gradpsi,v,psi,zsol) 
	caloptimalidad(gradv,gradpsi,optimalidad) 
	cout << "Current Grad norm == " << optimalidad << endl;

	
    real gpsinorm = gamma*gamma*int2d(ThSpacetime)(gradpsi^2);	   
    real gvnorm = l*l*int2d(ThSpacetime,FollowerRegionLabel,IntersecRegionLabel)(gradv^2);
    iterationnum++;

		
	func real[int]  DJJV(real[int] &X){
		FeSpacetimeControlF vaux =v;  
		descenso(vaux,gradv,X[0]);
		SolUvarform(vaux,psi,hs);
		SolZvarform(uua,uobj);
		FillspaceElement(zsol,zza);
		FeSpacetime gradvaux;
		gradienteV(gradvaux,vaux,zsol);
		real[int] valor(1);
		valor[0] = -int2d(ThSpacetime,FollowerRegionLabel,IntersecRegionLabel)(gradvaux*gradv);
		return valor;
		}
		
	func real[int]  DJJPSI(real[int] &X){
		FeSpacetime psiaux =psi;
		acenso(psiaux,gradpsi,X[0])
		SolUvarform(v,psiaux,hs);		
		SolZvarform(uua,uobj);
		FillspaceElement(zsol,zza);
		FeSpacetime gradpsiaux;
		gradientePSI(gradpsiaux,psiaux,zsol);
		real[int] valor(1);
		valor[0] = -int2d(ThSpacetime)(gradpsiaux*gradpsi);
		return valor;
		}	
		
   func real[int] CV(real[int] &X){
   if(gvnorm > 1.0)	   
   X[0] = X[0]/gvnorm;
   return X; 
   }
   
   func real[int] CPSI(real[int] &X){
   if(gpsinorm > 1.0)	   	   
   X[0] = X[0]/gpsinorm;
   return X; 
   }
   
    cout << "NLCG for follower step" << endl;
    NLCG(DJJV, pasov, precon=CV , nbiter=10, eps=-1.0e-4);
    cout << "NLCG for the pertubation step" << endl;
    NLCG(DJJPSI, pasopsi, precon=CPSI , nbiter=10, eps=-1.0e-4);


   descenso(v,gradv,pasov[0]);
   acenso(psi,gradpsi,pasopsi[0])
		


	cout << " follower step =  " << pasopsi[0] << endl;
	cout << " pertubation step = " << pasov[0] << endl;
	if( (optimalidad < optitol*0.001 )  ){
		salir =1;
		}
	} 
 
}
//////////////////////////////////////////////////
//////////////////////////////////////////////////


/* The gradiant of J with respect of h*/
func real[int] dJH (real[int] & hhaux){
   FeSpacetimeControlL hh;
   hh[] = hhaux;
   
   //resolver el nonlinear robust control
   NonLinearRobustControl(hh);
   
   //Obteniendo U con el punto silla v y psi
   SolUvarform(v,psi,hh);
   
   //Obteniendo Z
   SolZvarform(uua,uobj);  
   
   //Resolver el dual phi1,phi2 calculando k=vdual,s=psidual
   LinearCGRobustControl(vdual,psidual,uua,uulapa,zza,Uobjtivo,Uobjtivoxx);
   
   //Obteniendo phi1 mediante el punto silla k=vdual,s=psidual
   Solphi1varform(uua,vdual,psidual);
   
   //Obteniendo phi2  
   Solphi2varform(beta1,uua,uulapa,zza,Uobjtivo,Uobjtivoxx,phi1a);
   
   //Pasando de un arreglo de elementos a un elemento en espacio-tiempo
   //phi2a es un arreglo de elementos donde phi2a[i] = phi2sol(x,(i+1)*dt) 
   FillspaceElement(phi2sol,phi2a);

   //definiendo al elemento del gradiente
   FeSpacetimeControlL ggradH;
   
   //Calculando el gradiente
   ggradH = beta2*hh-phi2sol;
   return ggradH[]; //warning: no return of local array
}

NLCG(dJH, h[], nbiter=50, eps=-optitol);


/*
plot(h,wait=1,fill=1,dim=3);
plot(v,wait=1,fill=1,dim=3);
plot(psi,wait=1,fill=1,dim=3);
*/
SolUvarform(v,psi,h);
FillspaceElement(usol,uua);




///////////////////////////Guardando todo///////////////////////

{
ofstream Saveparam(dirsave + "/parametros.txt"); 
Saveparam << "mu " << mu << endl;
Saveparam << "l " << l << endl;
Saveparam << "gamma " << gamma << endl;
Saveparam << "dt " << dt << endl;
Saveparam << "Tfinal " << Tfinal << endl;
Saveparam << "beta1 " << beta1 << endl;
Saveparam << "beta2 " << beta2 << endl;
}


//guardando los estados
exec("mkdir " + dirsave + "/Estados");


//Guardando el error del control
{
string filename1 = "/Estados/T_error"  + ".txt";
ofstream file(dirsave + filename1);
for (int i=0;i<maxiteration;i++) {
	FeOmega uobjslice = Uobjtivo(x,(i+1)*dt); 
    file <<(i+1)*dt <<" " << int2d(Th)((uua[i]-uobjslice)^2.0)/meshheight << endl; 
}		
}

{
string name = dirsave + "/Estados/" + "usol_matlab.txt" ;  
ofstream savedataa(name);
for(int i=0 ; i < ThSpacetime.nv; i++ )
savedataa.showbase << ThSpacetime(i).x << " " << ThSpacetime(i).y <<" "<< usol[][i] << endl;;
}

{
string name = dirsave + "/Estados/" + "usol_freefem.txt" ;  
ofstream savedataa(name);
savedataa << usol[];
}



//guardando la pertubacion
exec("mkdir " + dirsave + "/pertubacion");
{
	string name = dirsave + "/pertubacion/" + "psi_sol.txt";  
	ofstream file(name);
	file << psi[];
}
{
	string name = dirsave + "/pertubacion/" + "psi_sol_matlab.txt";  
	ofstream file(name);
	for(int i=0 ; i < ThSpacetime.nv; i++ )
	file.showbase << ThSpacetime(i).x << " " << ThSpacetime(i).y <<" "<< psi[][i] << endl;;

}


//guardando el seguidor
exec("mkdir " + dirsave + "/seguidor");

{
	string name = dirsave + "/seguidor/" + "v_sol.txt";  
	ofstream file(name);
	file << v[];
}

//seguidor en todo el espacio
FeSpacetime vfullspace = v*chi(ThSpacetimeControlF); 

{
	string name = dirsave + "/seguidor/" + "vfull_sol_matlab.txt";  
	ofstream file(name);
	for(int i=0 ; i < ThSpacetime.nv; i++ )
	file.showbase << ThSpacetime(i).x << " " << ThSpacetime(i).y <<" "<< vfullspace[][i] << endl;;

}

//guardando el lider
exec("mkdir " + dirsave + "/lider");

{
	string name = dirsave + "/lider/" + "h_sol.txt";  
	ofstream file(name);
	file << v[];
}

//lider en todo el espacio
FeSpacetime hfullspace = h*chi(ThSpacetimeControlL); 

{
	string name = dirsave + "/lider/" + "hfull_sol_matlab.txt";  
	ofstream file(name);
	for(int i=0 ; i < ThSpacetime.nv; i++ )
	file.showbase << ThSpacetime(i).x << " " << ThSpacetime(i).y <<" "<< hfullspace[][i] << endl;;

}


//Guardando loss datos para  despues
savemesh(ThSpacetime,dirsave + "/ThSpacetime.msh"); // freefem
savemesh(ThSpacetimeControlF,dirsave + "/ThSpacetimeControlF.msh"); // freefem
savemesh(ThSpacetimeControlL,dirsave + "/ThSpacetimeControlL.msh"); // freefem






 
